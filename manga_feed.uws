
call scrapers.uws

const out_xml = ".\docs\index.xml"
const channel_json = ".\channel.json"
const log_file = ".\manga_feed.log"
scrapers = get_scrapers()

channel = load_channel()
remove_old_item(channel)
if (cnt := check_for_update(scrapers, channel)) > 0 then
    save_channel(channel)
    gen_rss_and_push2(cnt)
endif
log(cnt)


function load_channel()
    if fopen(channel_json, F_EXISTS) then
        json = fget(fopen(channel_json, F_READ or F_AUTOCLOSE), F_ALLTEXT)
        result = fromjson(json)
        if result == EMPTY then
            result = @{
                "title": "Webまんがフィード",
                "link": "https://github.com/stuncloud/manga_feed",
                "pubDate": null,
                "description": "Webまんがフィードを自力で作っていくやつ",
                "items": []
            }@
        endif
    endif
fend
procedure save_channel(channel: uobject)
    channel.pubDate = PubDate().to_string()
    json = tojson(channel, true)
    fid = fopen(channel_json, F_WRITE8)
    fput(fid, json, F_ALLTEXT)
    fclose(fid)
    sleep(0.5)
fend

procedure remove_old_item(channel: uobject)
    // 30日経過したアイテムは除去
    limit = gettime(-30, , G_OFFSET_DAYS)
    items = Filter(channel.items).by(|item => item.pubsec > limit|)
    channel.items = items
fend

function check_for_update(scrapers, channel: uobject)
    result = 0
    for scraper in scrapers.as_array()
        title = scraper.title()
        if scraper.check() then
            print_now("checking <#title>")
            if item := scraper.scrape() then
                next_check = scraper.get_check_fmt()
                if type_of(item) == TYPE_ARRAY and length(item) > 0 then
                    log("<#title> が更新されました、次回は<#next_check>以降に更新確認を行います")
                    // print_now(item)
                    for _item in item
                        channel.items += _item
                        result += 1
                    next
                else
                    log("<#title> が更新されました、次回は<#next_check>以降に更新確認を行います")
                    // print_now(item)
                    channel.items += item
                    result += 1
                endif
            else
                log("更新なし: <#title>、次回は<#next_check>以降に更新確認を行います")
            endif
        endif
    next
fend
procedure gen_rss_and_push2(cnt)
    // print_now("更新数: <#cnt>")
    res = shexec("rssgenexe\target\release\rssgenexe.exe", "<#channel_json> <#out_xml>")
    if res then
        repeat
            sleep(0.2)
        until fopen(out_xml, F_EXISTS)
        // print_now("rss生成: 成功")
        push_to_github(out_xml, cnt)
    else
        // print_now("rss生成: 失敗")
        log(0, "rss生成失敗")
        // log = print_now("エラー(<#error>)")
        // fopen(log_file, F_APPEND, log)
    endif
fend

function print_now(msg)
    now = format(gettime(), '%F %T')
    result = "<#now>: <#msg>"
    print result
fend

procedure push_to_github(xml, cnt)
    // print_now("push処理開始")
    sleep(1)
    print add := doscmd("git add <#xml>")
    sleep(1)
    print commit := doscmd("git commit -m <#DBL><#cnt>件更新<#DBL>")
    sleep(1)
    print push := doscmd("git push")
    log_git(add, commit, push)
    // print_now("push処理終了")
fend

procedure log(msg, error: bool = FALSE)
    now = format(gettime(), '%F %T')
    if error then
        fopen(log_file, F_APPEND, "<#now> [ERR] <#error>")
    else
        if type_of(msg) = TYPE_NUMBER then
            cnt = msg
            if cnt > 0 then
                fopen(log_file, F_APPEND, "<#now> [LOG] <#cnt> 件更新")
            else
                fopen(log_file, F_APPEND, "<#now> [LOG] 更新なし")
            endif
        else
            fopen(log_file, F_APPEND, "<#now> [LOG] <#MSG>")
        endif
    endif
fend
procedure log_git(add, commit, push)
    now = format(gettime(), '%F %T')
    append = | log => fopen("git.log", F_APPEND, "<#log><#CR>") |
    append(now)
    append(add)
    append(commit)
    append(push)
fend


class Filter
    dim arr
    procedure Filter(arr: uobject)
        this.arr = arr
    fend
    function by(fn: func)
        // https://github.com/stuncloud/UWSCR/issues/275 対策
        result = @[]@
        for item in arr
            if fn(item) then
                result += item
            endif
        next
    fend
endclass

// def_dll generate(wstring, wstring, var wstring):bool:rssgen\target\release\rssgen.dll
