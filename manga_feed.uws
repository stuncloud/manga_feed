
call scrapers.uws

const out_xml = "docs\index.xml"
const channel_json = "channel.json"
const log_file = "manga_feed.log"
scrapers = get_scrapers()

print_now("Begin manga_feed")
select PARAM_STR[0]
    case '--immediate', '-i'
        print_now("初回チェック")
        check_immediately(scrapers)
        print_now("初回チェック完了")
selend
print_now("監視開始")
while TRUE
    repeat
        sleep(300)
        gettime()
    until 0 <= G_TIME_NN and G_TIME_NN <= 5 // n時0分から5分の間になるまで待つ
    check_on_schedule(scrapers)
wend

procedure check_on_schedule(scrapers)
    channel = load_channel()
    remove_old_item(channel)
    cnt = check_for_update(scrapers, channel)
    // gen_rss_and_push(channel, cnt)
    save_channel(channel)
    gen_rss_and_push2(cnt)
fend

procedure check_immediately(scrapers)
    channel = load_channel()
    remove_old_item(channel)
    cnt = check_for_update(scrapers, channel, true)
    // gen_rss_and_push(channel, cnt)
    save_channel(channel)
    gen_rss_and_push2(cnt)
fend

function load_channel()
    if fopen(channel_json, F_EXISTS) then
        json = fget(fopen(channel_json, F_READ or F_AUTOCLOSE), F_ALLTEXT)
        try
            result = fromjson(json)
        except
        finally
            result = @{
                "title": "Webまんがフィード",
                "link": "https://github.com/stuncloud/manga_feed",
                "pubDate": null,
                "description": "Webまんがフィードを自力で作っていくやつ",
                "items": []
            }@
        endtry
    endif
fend
procedure save_channel(channel: uobject)
    channel.pubDate = PubDate().to_string()
    json = tojson(channel, true)
    fput(fopen(channel_json, F_WRITE8 or F_AUTOCLOSE), json, F_ALLTEXT)
fend

procedure remove_old_item(channel: uobject)
    // 90日経過したアイテムは除去
    limit = gettime(-90, , G_OFFSET_DAYS)
    items = Filter(channel.items).by(|item => item.pubsec > limit|)
    channel.items = items
fend

function check_for_update(scrapers, channel: uobject, ignore_check = FALSE)
    gettime()
    result = 0
    for scraper in scrapers.as_array()
        title = scraper.title()
        if ignore_check or scraper.check(G_TIME_WW, G_TIME_HH) then
            print_now("checking <#title>")
            if item := scraper.scrape() then
                if type_of(item) == TYPE_ARRAY and length(item) > 0 then
                    print_now("<#title> が更新されました")
                    print_now(item)
                    for _item in item
                        channel.items += _item
                        result += 1
                    next
                else
                    print_now("<#title> が更新されました")
                    print_now(item)
                    channel.items += item
                    result += 1
                endif
            else
                print_now("更新なし: <#title>")
            endif
        endif
    next
fend
// procedure gen_rss_and_push(channel, cnt)
//     print_now("更新数: <#cnt>")
//     if cnt > 0 then
//         channel.pubDate = PubDate().to_string()
//         error = format(chr(0), 512)
//         if generate("<#channel>", out_xml, error) then
//             print_now("rss生成: 成功")
//             push_to_github(out_xml, cnt)
//         else
//             print_now("rss生成: 失敗")
//             log = print_now("エラー(<#error>)")
//             fopen(log_file, F_APPEND, log)
//         endif
//     endif
// fend
procedure gen_rss_and_push2(cnt)
    print_now("更新数: <#cnt>")
    if cnt > 0 then
        res = shexec("rssgenexe\target\release\rssgen.exe", "<#channel_json> <#out_xml>")
        if res then
            print_now("rss生成: 成功")
            push_to_github(out_xml, cnt)
        else
            print_now("rss生成: 失敗")
            // log = print_now("エラー(<#error>)")
            // fopen(log_file, F_APPEND, log)
        endif
    endif
fend

function print_now(msg)
    now = format(gettime(), '%F %T')
    result = "<#now>: <#msg>"
    print result
fend

procedure push_to_github(xml, cnt)
    print_now("push処理開始")
    print doscmd("git add <#xml>")
    print doscmd("git commit -m <#DBL><#cnt>件更新<#DBL>")
    print doscmd("git push")
    print_now("push処理終了")
fend

class Filter
    dim arr
    procedure Filter(arr: uobject)
        this.arr = arr
    fend
    function by(fn: func)
        result = []
        for item in arr
            if fn(item) then
                result += item
            endif
        next
    fend
endclass

// def_dll generate(wstring, wstring, var wstring):bool:rssgen\target\release\rssgen.dll
